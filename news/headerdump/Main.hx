// Import all class needed.
import haxe3ds.Console;

import haxe3ds.services.APT;
import haxe3ds.services.FS;
import haxe3ds.services.GFX;
import haxe3ds.services.HID;
import haxe3ds.services.News;

import haxe3ds.stdutil.FSUtil;

// Main Function, this is where your program starts.
function main() {
	// Initialize all libraries for setup.
	GFX.initDefault();
	News.init();
	FS.init();
	Console.init(GFX_TOP);

	// Print to show that user is dumping all the news stored in news.db
	Sys.println('Dumping all news database...');

	/**
	 * Checks if dir exists then deletes it, and create the necessary directories so that it won't create errors.
	 * 
	 * /newsDump/ is where it's gonna be located.
	 * /newsDump/mpo/ is where all the images that was listed will be dumped there.
	 */
	if (FSUtil.isDirectory("sdmc:/newsDump")) FS.deleteDir("/newsDump", true);
	FSUtil.createDirectory("sdmc:/newsDump/mpo/");

	// Open and create the file, cause we would love to see its header names and some other data stuff :)
	final dumper:FSFile = new FSFile("/newsDump/output.txt");

	// Resize file to 0 bytes to remove any existing data, do you even wanna see the random jumbled mess?
	dumper.resize(0);

	// Header File so we know which caused it.
	dumper.write("Generated by Haxe3DS @ https://github.com/Haxe3DS/\n\n");

	// Get the total amount of news found.
	final total:Int = News.totalNotifications;

	// Go through a loop so we get the headers and yeah.
	for (i in 0...total) {
		// Get the news header from index i, because it has some cool data stored in there.
		final data:NEWSHeader = News.getHeader(i);

		// Write the whole data in the dump file, i'm excited.
		dumper.write('Database $i:\n');
		dumper.write('\t- dataSet:      ${data.dataSet}\n');
		dumper.write('\t- unread:       ${data.unread}\n');
		dumper.write('\t- enableJPEG:   ${data.enableJPEG}\n');
		dumper.write('\t- isSpotPass:   ${data.isSpotPass}\n');
		dumper.write('\t- isOptedOut:   ${data.isOptedOut}\n');
		dumper.write('\t- processID:    ${data.processID}\n');
		dumper.write('\t- jumpParam:    ${data.jumpParam}\n');
		dumper.write('\t- time (by ms): ${data.time}\n');
		dumper.write('\t- title:        ${data.title}\n');
		dumper.write('\t- result:       ${data.result}\n');
		dumper.write('\t- Dumping RC:   ${News.dumpImage(i, '/newsDump/mpo/$i.mpo')}\n\n');

		// Print to show progress (which is i / total).
		Sys.println('Dumped Database $i! (${i+1}/$total)');
	}

	// Finishing touches, close the file and then few prints.
	dumper.close();
	Sys.println("Done dumping all databases.");
	Sys.println("Press [START] to exit.");

	// Main Loop.
	while (APT.mainLoop()) {
		// Checks if key [START] is pressed.
		if (HID.keyPressed(Key.START)) {
			break; // Break loop.
		}
	}

	// Exits all Services and exits application/3dsx.
	FS.exit();
	News.exit();
	GFX.exit();
}
